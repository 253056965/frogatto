{
id: "item_acquirable",
prototype: ["standard_values"],
zorder: "@include data/zorder.cfg:player",
respawns: false,
collide_dimensions: ["~item"],

properties: {
	attractable: "false",  //categorically speaking, can this get pulled in by the "magnetic" attractor powerup?
	
	_should_attract: "(attractable and _is_inside_attraction_range and level.player.has_item('acquirable_attractor'))",
	_has_started_attracting: "bool :: (not _started_attracting_at_cycle = null)",
	_started_attracting_at_cycle: { type: "int|null", default: null },
	_is_inside_attraction_range: "(distance(me,level.player) < 200)",
	_angle_to_player: "lib.math.angle(me, level.player)",
	_attraction_speed: "50.0",
	
	
	process_attract: "if(_has_started_attracting and (not vars.been_acquired),
			[	
				add(velocity_x, x_diff) where x_diff = _attraction_speed * cos(_angle_to_player) * 1.0,
				add(velocity_y, y_diff) where y_diff = _attraction_speed * sin(_angle_to_player) * 1.0,
				
				add(mid_x, x_diff) where x_diff = _attraction_speed/10 * cos(_angle_to_player) * 1.0,
				add(mid_y, y_diff) where y_diff = _attraction_speed/10 * sin(_angle_to_player) * 1.0,
				
				if(level.cycle - _started_attracting_at_cycle > 200, fire_event('acquired')),
			])",
},

on_process: "[if(_should_attract, set(_started_attracting_at_cycle,level.cycle)), process_attract]",

editor_info: {
	category: "props",
},
vars: {
	been_acquired: 0,
},

on_end_normal_anim: "animation('normal')",

on_collide_object_item: "if(collide_with.is_player_body_part and (not vars.been_acquired),fire_event('acquired'))",


// expected behaviors:
// on_acquired: "/* virtual; each object will need almost completely custom behavior for what it actually *does* when it's acquired, however we may want to unify the visual-effects stuff here. */",
// animation should have "item" area

}
