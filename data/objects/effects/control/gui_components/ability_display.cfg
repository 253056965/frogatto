//Show the currently selected ability, such as 'tongue' or 'firebreath'.
{
	id: "ability_display",
	is_strict: true,
	use_absolute_screen_coordinates: true,
	hidden_in_game: true,
	serializable: false,
	
	zorder: "@include data/zorder.cfg:in_front_of_everything",

	properties: {
		target: { type: "obj frogatto_playable" },
		
		//Generate a widget for each ability to display, to be swapped out as needed.
		ability_display: {
			type: "[widget]", 
			init: "[widget(me, {
				type: 'image', 
				image: 'gui/hud.png', 
				area: img_rect, 
			}) | img_rect <- [
				[79,36,98,55], //tongue grab
				[53,36,72,55], //fire breath
				[27,36,46,55], //energy bullet
				[53,62,72,81], //homing shot
				[79,62,98,81], //acid spit
			]]",
		},
		
		last_displayed_ability: { type: "int", default: 0 },
	},
	
	on_create: "set(me.widgets.child, ability_display[0])",
	
	on_process: "if(last_displayed_ability != target.current_ability, [
		set(last_displayed_ability, target.current_ability),
		set_widgets(ability_display[target.current_ability]),
	])"
}

/* PREVIOUSLY, IN hud.cfg:
	//// The portraitsâ€¦
	if(level.local_player,
		if(level.local_player is obj frogatto_playable,
			if(is_int(level.local_player.current_ability),
				switch(level.local_player.ability_list[level.local_player.current_ability],
					   'tongue',     draw_animation('frogatto_tongue_portrait',     10, 10),
					   'firebreath', draw_animation('frogatto_firebreath_portrait', 10, 10),
					   'energyshot', draw_animation('frogatto_energyshot_portrait', 10, 10),
					   'homingshot', draw_animation('frogatto_homingshot_portrait', 10, 10),
					   'acidbreath', draw_animation('frogatto_acidbreath_portrait', 10, 10),
									 draw_animation('frogatto_portrait',            10, 10))))),
*/