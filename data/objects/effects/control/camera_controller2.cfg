{
id: "camera_controller2", //2, a strict-mode version
next_animation: "'normal'",
always_active: true, //Hack until we figure out the appropriate bounding box.
hidden_in_game: true,
zorder: "@include data/zorder.cfg:in_front_of_everything",


editor_info: {     
	category: "controllers",
	help: "When triggered, this controller moves the camera through a preset path. Once it reaches its
destination, it fires the triggered event of the object with the ID given by next_id. For use with
scripting, please see the setup() and go() functions as defined in camera_controller.cfg.

variable        | description
--------------------------------
passepartout    | If the object is to be used in the editor, should it preview the camera area?
preview route   | If the object is to be used in the editor, should the preview move along the route?
speed           | How fast the camera moves. This number is related to 'percentage along camera path'.
constant speed  | Depreciated. Set to 'yes' to stop the camera from picking up speed as it goes along.
tween type      | One of ['linear', 'sin_in_out', 'sin_in', 'sin_out']. The functions are defined in the math library.
path            | This value is just passed to set_path().
average path    | Should the 'path' points be considered control points for _the_ bezier curve?
linked object   | Works like a button's linked object, but is fired when the camera is done moving.
start_with_player_as_first_node | Defaults to true, if false snaps to the first position in the path.
See the set_options FFL function in ~/modules/frogatto/data/objects/effects/control/camera_controller.cfg for more.
Note: Rope preview and passepartout only works with editor unpaused.",
},


properties: {
	//Editor Properties:
	"passepartout": {type: "string", editor_info: {type: "enum", value: "'standard'", enum_values: ["none", "standard", "iPhone", "iPad"],},},
	"preview route": {type: "bool", editor_info: {type: "boolean", value: "false",},},
	"speed": {type: "int", editor_info: {type: "int", value: 10, min_value: 1, max_value: 100,},},
	"tween type": {type: "string", editor_info: {type: "enum", value: "'sin_in_out'", enum_values: ["linear", "sin_in_out", "sin_in", "sin_out"],},},
	"path": {type: "[Point2d]", editor_info: {type: "points", value: "[]",},},
	"average path": {type: "bool", editor_info: {type: "boolean", value: "false",},},
	"target zoom": {type: "decimal", editor_info: {type: "decimal", value: 1, min_value: 0.25, max_value: 7.5,},},
	"linked_object": {type: "string|null", editor_info: {type: "label",},},
	"release control": {type: "bool", editor_info: {type: "boolean", value: "false",},},
	"start_with_player_as_first_node": {type: "bool", editor_info: {type: "boolean", value: "true",},},
	
	//Vars:
	preview_rope_controller: {type: "obj rope_controller|null", default: null,},
	last_preview_path: {type: "[Point2d]|null", default: null,},
	last_preview_smooth: {type: "bool", default: false,},
	last_preview_route: {type: "bool", default: false,},
	linked_object_list: {type: "[obj triggerable]", default: [],},
	linked_delay: {type: "int", default: 1,},
	
	//Non-editor Properties:
	
	//Called each frame. Returns the path to draw the purple arrow ribbon along.
	new_preview_path: "[[decimal]] <- if(start_with_player_as_first_node, [tmp.starting_camera_position or lib.standardize.camera(level)], []) + self.path + if(self['release control'], [lib.standardize.natural_camera(level)], [])",


	do_rope_preview: "def() -> commands [
		set(last_preview_path, new_preview_path),
		set(last_preview_smooth, self['average path']),
		if(rope_controller != preview_rope_controller, [
			if(preview_rope_controller, self.remove_rope_controller(preview_rope_controller)),
			self.add_rope_controller(rope_controller),
			rope_controller.set_points(new_preview_path),
		],[
			if(last_preview_path != new_preview_path or last_preview_smooth != self['average path'],
				rope_controller.set_points(new_preview_path))
		]),
	] where rope_controller = if(last_preview_smooth = self['average path'] and preview_rope_controller, 
		preview_rope_controller, //Use the cached object if it's valid.
		if(self['average path'],
			object('curvy_rope_controller', 0,0,0, {desired_rope_type:'arrow ribbon'}), 
			object('${self.type}.preview_rope_controller')) )",
	
	
	add_rope_controller: "def(object rope_ctrl) -> commands [
		debug('added ' + cycle),
		add_object(rope_ctrl),
		set(preview_rope_controller, rope_ctrl),
		set(rope_ctrl.x, -10000), set(rope_ctrl.y, -10000), 
	]",
	
	remove_rope_controller: "def(object rope_ctrl) -> commands [
		debug('removed ' + cycle),
		set(preview_rope_controller, null),
		rope_ctrl.remove(),
	]",
	
	
	draw_border: "def([decimal] cam, decimal screen_width, decimal screen_height) -> commands if(not level.is_paused, [
		debug_rect(cam[0]-screen_width/2, cam[1]-screen_height/2, screen_width, -100000),
		debug_rect(cam[0]-screen_width/2, cam[1]+screen_height/2, screen_width,  100000),
		debug_rect(cam[0]-screen_width/2, cam[1]-100000, -100000, 300000),
		debug_rect(cam[0]+screen_width/2, cam[1]-100000, 100000, 300000),
	])",
	
	
	position_along_path: "overload(
		def([[decimal]] path, decimal percentage) calc(path, percentage),
		def([[decimal]|map|object] path, decimal percentage) calc(lib.standardize.toPoints(path), percentage)
	) where calc = def(list path, decimal percentage) if(self['average path'],
		lib.math.bezier_curve(path, real_percentage(percentage)),
		lib.math.interpolate(path, real_percentage(percentage))
	) where real_percentage = def(percentage) lib.math[me['tween type'] or 'sin_in_out'](percentage)",
	
	
	//Called after the camera is done moving. Delayable.
	trigger_next_object: "def() [
		if(get_object(level, linked_object or '') and (not level.in_editor), fire_event(get_object(level, linked_object), 'triggered')), 
		if(linked_object_list, map(linked_object_list, 'obj', fire_event(get_object(level, obj), 'button_pressed'))),
		fire_event(self, 'cut'),
		map(tmp.callback, value()),
	]",
	
	
	//Setup: Takes a list of optionally mixed list [x,y]s, {mid_x:x, mid_y:y}s, or objects. These act as control points that the camera will move by. Level.camera_position is always the implicit first control point, and doesn't/can't be set here.
	set_path: "def([[decimal]|map|object] new_path) -> commands
		set(me.path, lib.standardize.toPoints(new_path)) asserting size(new_path)",
		
		
	set_options: "def({string -> any} opt) -> commands [
		/*	key             | value
			--------------------------------
			passepartout    | If the object is to be used in the editor, should it preview the camera area?
			preview route   | If the object is to be used in the editor, should the preview move along the route?
			speed           | How fast the camera moves. This number is related to 'percentage along camera path'.
			constant speed  | Depreciated. Set to 'yes' to stop the camera from picking up speed as it goes along. Good for tenser, jumpy cutscenes.
			tween type      | One of ['linear', 'sin_in_out', 'sin_in', 'sin_out']. The functions are defined in the math library.
			path            | This value is just passed to set_path().
			average path    | Should the 'path' points be considered control points for a bezier curve? Note that all points are just averaged into one curve.
			linked object   | or linked_object. A string denoting the label of the object that should have the 'button_pressed' event fired when the camera reaches the end of the path.
			linked delay    | The number of frames to wait until firing the button_pressed event for the linked object and on_cut for ourself.
			callback        | Either a function or a list of functions to be called when the camera is done moving. Called before on_cut is fired.
			release control | When the camera is done, release control of the camera back to the level.
			start_with_player_as_first_node | Defaults to true, if false snaps to the first position in the path.
		*/
		
		//You can set some of the following vars from the graphical editor.
		if('passepartout' in ok, set(self['passepartout'], newval asserting newval in ['none', 'standard', 'iPhone', 'iPad'] where newval = string <- opt['passepartout'])),
		if('preview route' in ok, set(self['preview route'], bool <- opt['preview route'])),
		if('speed' in ok, set(self['speed'], newval asserting newval>0 where newval = decimal <- opt['speed'])),
		if('tween type' in ok, set(self['tween type'], string <- opt['tween type'] asserting opt['tween type'] in keys(lib.math))),
		if(opt.path, set_path(opt.path)) asserting opt.path = null or size(opt.path),
		if('average path' in ok, set(self['average path'], bool <- opt['average path'])),
		if(opt.linked_object,    set(self.linked_object,    string <- opt.linked_object   )) asserting opt.linked_object    = null or size(opt.linked_object   ),
		if(opt['linked object'], set(self['linked object'], string <- opt['linked object'])) asserting opt['linked object'] = null or size(opt['linked object']), //For consistency. Didn't want the linked_object to be the only var with an underscore... but at the same time, every other linked_object has an underscore.
		if('release control' in ok, set(self['release control'], bool <- opt['release control'])),
		if('start_with_player_as_first_node' in ok, set(self['start_with_player_as_first_node'], bool <- opt['start_with_player_as_first_node'])),
		if('linked delay' in ok, set(self['linked delay'], decimal <- opt['linked delay'] asserting opt['linked delay'] >= 0)),
		if(opt.callback, if(is_list(opt.callback), add(tmp.callback, opt.callback), add(tmp.callback, [opt.callback] asserting is_function(opt.callback)))),
	] where ok = keys(opt)",
		
		
	go: "def(any|null callback) [
		fire_event(self, 'start_sequence'),
		if(callback, add(tmp.callback, [callback])),
	]",
	
	
	add: "def() add_object(self)",
	
	remove: "def() [ //Removes the camera controller cleanly, releasing any locks it's created.
		if(self.preview_rope_controller, self.remove_rope_controller(self.preview_rope_controller)),
		remove_object(self),
		set(level.lock_screen, null), 
	]",
},


tmp: {
	travel_time: 0,
	starting_camera_position: null,
	starting_camera_zoom: null,
	callback: [],
},

//This could be fired more often, like, for instance, when we move a point variable in the level. As it is, we must check in on_process or on_draw for such changes.
on_editor_changed_variable: "[
	set(last_preview_route, self['preview route']),
	if(self['preview route'] and (not last_preview_route), fire_event('start_preview')),
]",


on_create: "[
	//We can specify our path via a string in the level file. This lets us reference other objects in the level, which we can't do from FML.
	if(typeof(me.path) = 'string', set(me.path, lib.standardize.toPoints(eval(me.path)))),
	
	//Check the depreciated variable 'constant speed' is not being used. (This check can be removed after 2013.)
	(null asserting not self['constant speed'] | 'Constant speed is ' + self['constant speed'] + ', but is obsolete and should not be used. Instead, set `tween type` to `linear`.')
]",


on_draw: "if(self in level.editor_selection, 
	[
		//First on_draw function: If we're selected, draw a moving passepartout and a line of arrows to show where we're going to go.
		//This is in on_draw, as opposed to on_process, because it removes the double line effect but seems to only do so by not applying the rotation/scale to the object, producing hanging-down arrows.
		switch(passepartout,
			'standard', draw_border(cam_pos, 800,600),
			'iPhone', draw_border(cam_pos, 480*2,320*2), //Double-res there.
			'iPad', draw_border(cam_pos, 1024,768),
			null) where cam_pos = position_along_path(new_preview_path, tmp.travel_time),
		do_rope_preview(),
		set(tmp.starting_camera_position, null),
	
	], [
		//If we're not editing the object, then we'll remove the preview stuff for it. The passepartout will remove itself, but we'll have to manually clean up the rope object.
		if(preview_rope_controller, remove_rope_controller(preview_rope_controller)),
		
		//Now, if we're actually 'going' for real, we'll set the camera position and increment the camera travel time.	
		//This is done in on_draw, because we need to catch the level before the first frame if we're not starting on Frogatto himself. This is used for the *dramatic* pan-in to the scene.
		if(tmp.going, [
			lib.standardize.set_camera(level, position_along_path(new_preview_path, tmp.travel_time)),
			set(level.zoom, lib.math.interpolate(tmp.starting_camera_zoom, tmp.travel_time, me['target zoom'])),
			
			set(tmp.travel_time, tmp.travel_time + me.speed/2000.0),
			if(tmp.travel_time > 1, [//If we've travelled the length of the camera guide, we'll stop going and trigger the next object.
				if(me['release control'],
					set(level.lock_screen, null),
					set(level.lock_screen, position_along_path(new_preview_path, tmp.travel_time)) ), 
				schedule(linked_delay, [
					set(tmp.going, false),
					trigger_next_object(),
				]),
			]),
		]),
	]
)",


//Recieve input from levers and triggers.
on_triggered:      "fire_event('start_sequence')",
on_button_pressed: "fire_event('start_sequence')",


//Reset some variables (we might start the sequence multiple times) and launch the main loop.
on_start_sequence: "[
	set(tmp.travel_time, 0),
	if(start_with_player_as_first_node,
		set(tmp.starting_camera_position, lib.standardize.camera(level)),
		set(tmp.starting_camera_position, path[0]) ),
	set(tmp.starting_camera_zoom, level.zoom),
	
	fire_event('roll'),
	set(tmp.going, true),
]",


on_start_preview: "[
	//Sets the camera to 'preview' mode, where we render a blue passepartout where the screen will be.
	set(tmp.travel_time, 0),
	fire_event('do_camera_preview'),
]",


on_do_camera_preview: "if(self['preview route'], [
	set(tmp.travel_time, tmp.travel_time + me.speed/2000.0),
	if(tmp.travel_time > 1, set(tmp.travel_time, 0)),
	if(self['preview route'], schedule(1+(tmp.travel_time>1)*19, fire_event('do_camera_preview'))),
])",


//Custom events, for use with scripting. These are split off into their own thing, so disaster doesn't strike if you forget to prototype them.
on_roll: "/*Your code here!*/", //Fired before the camera starts. You can set up the route here if you want.
on_cut:  "/*Your code here!*/", //Fired when the camera has reached its final resting place.


on_end_anim: "animation('normal')",

animation: {
	id: "normal",
	image: "effects/particles.png",
	x: 210,
	y: 228,
	w: 28,
	h: 28,
	collide: [0,0,28,28],
	frames: 1,
	duration: 1000,
},

object_type: { //This implements, minimally, the curvy_rope_controller but just draws ropes between the lines instead of curving them.
	id: "preview_rope_controller",
	next_animation: "'normal'",
	always_active: false,
	hidden_in_game: true,
	zorder: "@include data/zorder.cfg:in_front_of_everything",
	
	properties: {
		ropes: {type: "obj rope_controller", default: [],},
		
		//Used to draw curves for the rope preview in the editor. Strings new rope objects between the points passed in.
		set_points: "def(points) [
			set(ropes, new_ropes),                       //Remember the new ropes.
			map(ropes, remove_object(value)),            //Clean up the old ropes, if there are any.
			map(new_ropes, [
				add_object(value),
				set(value.always_active, true),          //Needed since we don't bother to set the activation rect on these.
				set(value.animation, 'arrow ribbon'), //Set the rope graphic.
			]),
		] where new_ropes = //First, list of all our new rope objects.
			map(
				map( //First, generate a list of each line segment between the verticies passed in.
					range(size(points)-1), 
					'point_index', 
					[points[point_index], points[point_index+1]] ),
				point,
				object('rope_angled_controller', 0, 0, 0, { //Then, make an object for each line segment.
					x_1: point[0][0], 
					y_1: point[0][1], 
					x_2: point[1][0], 
					y_2: point[1][1],
				}) )",
		
		//Remove this controller, and any rope objects it's spawned.
		remove: "def() map(ropes + [me], remove_object(value))",
	},
	
	on_end_anim: "animation('normal')",
	
	animation: {
		id: "normal",
		image: "effects/particles.png",
		x: 179,
		y: 104,
		w: 28,
		h: 28,
		collide: [0,0,28,28],
		frames: 1,
		duration: 1000,
	},
},

}