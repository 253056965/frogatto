//Show the currently selected ability, such as 'tongue' or 'firebreath'.
{
	id: "ability_display",
	is_strict: true,
	use_absolute_screen_coordinates: true,
	hidden_in_game: true,
	serializable: false,
	
	zorder: "@include data/zorder.cfg:in_front_of_everything",

	properties: {
		target: "obj frogatto_playable <- lp asserting lp where lp = level.local_player",
		
		ability_container: {
			type: "widget", 
			init: "widget(me, {
				type: 'image',
				image: 'gui/hud3.png',
				area: [80,0,80+48,0+47],
				xy: [-8,-8],
				zorder: lib.zorder.get('in_front_of_everything'),
			})",
		},
		
		//Generate a widget for each ability to display, to be swapped out as needed.
		ability_display: { type: "{string -> widget}", init: 
			"fold(map(ability_image_rects, { keys(ability_image_rects)[index] : 
					widget(me, {
						type: 'image', 
						image: 'gui/hud4.png', 
						area: img_rect, 
						zorder: lib.zorder.get('in_front_of_everything')-2,
						id: 'graphic',
						alpha: 255/3, // workaround for brightness flashes when switching between unpowered abilities.
					}) where img_rect = ability_image_rects[key] }),
				a + b)
				
				where ability_image_rects = {string -> [int,int,int,int]} :: {
								'tongue': 		[  0, 0, 39,39],
								'firebreath':	[ 42, 0, 81,39],
								'energyshot':	[ 84, 0,123,39],
								'homingshot':	[126, 0,165,39],
								'acidbreath':	[168, 0,207,39],
							}"
		},
		
		ability_container_background: {
			type: "widget", 
			init: "widget(me, {
				type: 'image',
				image: 'gui/hud3.png',
				area: [80,67,80+48,67+47],
				xy: [-8,-8],
				zorder: lib.zorder.get('in_front_of_everything')-3,
			})",
		},
		
		last_displayed_ability: { type: "string", default: 'invalid' },
	},
	

	on_create: "null asserting fold(map(ability_display, key in (obj frogatto_playable <- level.local_player).ability_list_full), a and b)",

	on_process: "if(last_displayed_ability != target.current_ability_name, [
		set(last_displayed_ability, target.current_ability_name),
		set_widgets([ability_container, ability_display[target.current_ability_name], ability_container_background]),
		// alpha is set by frogatto, depending on his mana level.
	])",
}

/* PREVIOUSLY, IN hud.cfg:
	//// The portraitsâ€¦
	if(level.local_player,
		if(level.local_player is obj frogatto_playable,
			if(is_int(level.local_player.current_ability),
				switch(level.local_player.ability_list[level.local_player.current_ability],
					   'tongue',     draw_animation('frogatto_tongue_portrait',     10, 10),
					   'firebreath', draw_animation('frogatto_firebreath_portrait', 10, 10),
					   'energyshot', draw_animation('frogatto_energyshot_portrait', 10, 10),
					   'homingshot', draw_animation('frogatto_homingshot_portrait', 10, 10),
					   'acidbreath', draw_animation('frogatto_acidbreath_portrait', 10, 10),
									 draw_animation('frogatto_portrait',            10, 10))))),
*/