//The mana bar displays how much energy for abilities we have.
{
	id: "mana_display",
	is_strict: true,
	use_absolute_screen_coordinates: true,
	hidden_in_game: true,
	serializable: false,
	
	zorder: "@include data/zorder.cfg:in_front_of_everything",
	no_compile_image: "gui/progress-bar-center.png",

	properties: {
		target: "obj player_controlled <- lp asserting lp where lp = level.local_player",
		
		mana_bar_max_length: "115*2",
		mana_bar_cap_width: 5,
		mana_bar_length: "round(((mana_bar_max_length - mana_bar_cap_width) * target.mana) / target.max_mana)",
	
		mana_bar_container: {
			type: "widget", 
			init: "widget(me, {
				type: 'image',
				image: 'gui/hud.png',
				area: [2,131,124,139], //xyxy
			})",
		},
		
		mana_bar_center: {
			type: "obj mana_display.bar_center",
			init: "object('mana_display.bar_center', x,y,1)",
		},
		mana_bar_left: {
			type: "obj mana_display.left_cap",
			init: "object('mana_display.left_cap', x,y,1)",
		},
		mana_bar_right: {
			type: "obj mana_display.right_cap",
			init: "object('mana_display.right_cap', x,y,1)",
		}
	},
	
	on_create: "[
		add_object(mana_bar_center),
		add_object(mana_bar_left),
		add_object(mana_bar_right),
		set(me.widgets.child, mana_bar_container),
	]",
	
	on_process: "[
		[
			set(mana_bar_left.x, x_offset), set(mana_bar_left.y, y_offset),
			set(mana_bar_right.x, x_offset + mana_bar_length+4), set(mana_bar_right.y, y_offset),
			if(mana_bar_length > 5, 
				[set(mana_bar_left.alpha,255), set(mana_bar_right.alpha,255), set(mana_bar_center.alpha,255), 
					mana_bar_center.set_draw_params( x_offset+10, y_offset, mana_bar_length-5)],
				[set(mana_bar_left.alpha,0), set(mana_bar_right.alpha,0), set(mana_bar_center.alpha,0)]
			)
		] where x_offset = 52 where y_offset = 32,
				 


	]",
	
	
	object_type: [
	{
		zorder: "@include data/zorder.cfg:in_front_of_everything",
		id: "bar_center",
		is_strict: true,
		use_absolute_screen_coordinates: true,
		properties: {
			_height: { type: "int", default: 10 },
			set_draw_params: "def(int new_x, int new_y, int new_height) -> commands [
									set(x, new_x),
									set(y, new_y),
									set(_height, new_height)
								]"
						

		},
		on_process: "set(draw_area, [0.0, 0.0, _height/2.0, img_h/2.0])",
		
		animation: {
			id:	'normal',
			image: 'gui/progress-bar-center.png',
			rect: [0,0,7,7], //xywh
		},
	},{
		zorder: "@include data/zorder.cfg:in_front_of_everything",
		id: "right_cap",
		is_strict: true,
		use_absolute_screen_coordinates: true,

		animation: {
			id:	'normal',
			image: 'gui/progress-bar-endcaps.png',
			rect: [0,8,5,15], //xywh
		},
	},{
		zorder: "@include data/zorder.cfg:in_front_of_everything",
		id: "left_cap",
		is_strict: true,
		use_absolute_screen_coordinates: true,

		animation: {
			id:	'normal',
			image: 'gui/progress-bar-endcaps.png',
			rect: [3,0,7,7], //xywh
		},
	}],
	
}

/* PREVIOUSLY, IN hud.cfg:
	//// The blue mana-bar.		                 
	draw_animation('manabar_empty', 48, 32),
	if(level.local_player and level.local_player.mana > 0, ([
		draw_animation('charge_bar_left', x_offset, y_offset),
		draw_animation('charge_bar_right', x_offset + barLength*2, y_offset),
		if(barLength > 5, draw_animation_area('charge_bar_center', x_offset+10, y_offset, barLength-5))
	] where x_offset = 52, 
	        y_offset = 32,
	        barLength = int((fullBarLength*level.local_player.mana)/level.local_player.max_mana))
	  where fullBarLength = 114 ),
*/