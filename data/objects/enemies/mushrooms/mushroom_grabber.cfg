{
id: "mushroom_grabber",

friction: 4000,
always_active: true,
prototype: ["ant_flying"],
zorder: "@include data/zorder.cfg:near_player_foreground_effects",
properties: {
	physical_size: 32,
	altitude_adjustment: "(wave(cycle*5+1000)/50)",
	handle_special_damage_response: "def(collide_with) if(final_damage_amount(collide_with.attack_damage) > 0, [set(vars.time_last_spooked, cycle), set(vars.behavior, 'climb')])",
	spook_duration: 200,
	is_spooked: "(cycle - vars.time_last_spooked < spook_duration)",
	
	dive_speed_x: "if(higher_difficulty, 30+1d30, 30)",
	dive_speed_y: "if(higher_difficulty, 30, 30)",
	climb_speed_x: "if(higher_difficulty, 15, 15)",
	climb_speed_y: "if(higher_difficulty, 12, 12)",
	
	should_dive: "if((not is_spooked) and can_seize_player,  if(
						level.player.y > y + 50 and level.player.y < vars.dive_height and
						level.player.midpoint_x > vars.x_bound and
						level.player.midpoint_x < vars.x2_bound, 1, 0))",

	should_climb: "if(midpoint_y > vars.dive_height or midpoint_y < vars.desired_height-20 or midpoint_x > vars.x2_bound or midpoint_x < vars.x_bound, 1, 0)",

	begin_dive: "def() [
								if(level.player.midpoint_x > midpoint_x, facing(1), facing(-1)),
								set(vars.behavior, 'dive'),
								set(vars.target_x, level.player.midpoint_x),
								set(vars.target_y, level.player.y),
								add(velocity_y,200)]",
	begin_climb: "def() [	set(vars.behavior, 'climb')]",
	process_dive: "def() [	if(level.player.midpoint_y > midpoint_y, add(velocity_y, dive_speed_y),add(velocity_y,-dive_speed_y) ),
								if(level.player.midpoint_x > midpoint_x, [try_facing(1),add(velocity_x, dive_speed_x)],[try_facing(-1),add(velocity_x,-dive_speed_x)] ),
							   	if(should_climb, begin_climb() )]",
	process_climb: "def() [	if(midpoint_x < init_x, [try_facing(1),add(velocity_x, climb_speed_x)],[try_facing(-1),add(velocity_x,-climb_speed_x)] ),
								if(midpoint_y < init_y, add(velocity_y, climb_speed_y),add(velocity_y,-climb_speed_y) ),
								if(should_hover, begin_hover()),
								]",
	begin_hover: "def() [	set(vars.behavior, 'hover')]",
	process_hover: "def() [	if(midpoint_x < init_x, [try_facing(1),add(velocity_x, climb_speed_x)],[try_facing(-1),add(velocity_x,-climb_speed_x)] ),
								if(midpoint_y < init_y, add(velocity_y, climb_speed_y),add(velocity_y,-climb_speed_y) ),
								if(should_dive, begin_dive())
									]",
	should_hover: "if(abs(midpoint_x - vars.init_x) < 100 and abs(midpoint_y - vars.init_y) < 100, 1, 0)",

	try_facing: "def(direction) if(direction != facing,fire_event('turn'))",

	death_fx_type: "'mushroom'",
	acquirable_item_drop_value: 10,					
	points_value: 14,
	
	central_process: "def() [if(is_carrying_player, carry_player),switch(vars.behavior,
							'', process_climb(),
							'hover', process_hover(),
							'dive', process_dive(),
							'climb', process_climb(),
							'regular', [proto_event('ant_flying', 'process_fly'), if(should_dive, begin_dive())]),
							set(rotate, sin(cycle*7) * 4.0),
							if(animation in ['fly1','fly2','fly3'], [set(animation, 'fly'+animation_suffix), set(time_in_animation, time_in_animation)])]",

	animation_suffix: "round(sin(cycle*4)*1.0+1.0)+1",  //smoothly oscillate between 1 -> 3

	springiness: 190,
	spring_amount: 1200,
	bounce_roundness: 1,
	"material sound": "fungus",
	
	carry_player: "[set(level.player.mid_x, me.mid_x),set(level.player.y, me.y+10),set(level.player.velocity_y, me.velocity_y)]",
	is_carrying_player: "((cycle - vars.last_seized) < seize_duration)",
	can_seize_player: "(cycle - vars.last_seized > seize_duration*2)",
	seize_player: "[set(vars.last_seized, cycle),set(vars.behavior,'climb')]",
	seize_duration:  150,
},
vars: { 
	last_seized: -150,
	time_last_spooked: -200,
},
on_collide_object_seize: "if((not is_carrying_player) and can_seize_player, [seize_player, sound('Mushroom-grab.wav')])", 

on_create: "[	if(vars.behavior = null,set(vars.behavior,'hover')),
						animation('fly1'),
						if(vars.init_x = null, set(vars.init_x, midpoint_x)),
						if(vars.init_y = null, set(vars.init_y, midpoint_y)),
					]",
on_end_anim: "animation('fly1')",
on_end_hurt_anim: "animation('fly1')",


on_turn: "if(cycle-vars.last_turned > 30,[set(vars.last_turned,cycle),set(facing, -facing)])", #symmetric creature, so overrule the usual turn behavior because we have no turn anim

on_process: "central_process()",
on_lose_wings: "#don't lose wings#",

on_wingflap: "[add(velocity_y,-150), sound_falloff('Jellyfish-Squish'+1d3+'.wav')]",

platform_area: [7,13,20,14],
on_bounced_on: "add(velocity_y,450)",

#solid_area=5,10,20,25
solid_area: null,
editor_info: {
	category: "enemies, forest",
	var: [
		{
			name: "x_bound",
			type: "x",
			value: "x - 200",
		},
		{
			name: "x2_bound",
			type: "x",
			value: "x2 + 200",
		},
		{
			name: "desired_height",
			type: "y",
			value: "midpoint_y",
		},
		{
			name: "dive_height",
			type: "y",
			value: "midpoint_y+300",
		},
	],
},
animation: [
	{
		"@base": true,
		image: "enemies/mushroom-grabber.png",
		body_area: "all",
		accel_y: 0,
		feet_x: 22,
		feet_y: 22,
		pad: 0,
		seize_area: [4,18,24,28],
	},
	{
		id: "fly1",
		rect: [0,0,44,44],
		frames: 8,
		frames_per_row: 4,
		duration: 5,
		accel_x: 0,
		events: "8:wingflap",
		seize_area: [4,18,24,28],
	},
	{
		id: "fly2",
		rect: [0,90,44,134],
		frames: 8,
		frames_per_row: 4,
		duration: 5,
		accel_x: 0,
		events: "8:wingflap",
		seize_area: [4,18,24,28],
	},
	{
		id: "fly3",
		rect: [0,180,44,224],
		frames: 8,
		frames_per_row: 4,
		duration: 5,
		accel_x: 0,
		events: "8:wingflap",
		seize_area: [4,18,24,28],
	},
	{
		id: "spring",
		rect: [0,90,44,134],
		frames: 1,
		duration: 3,
	},
	{
		id: "turn",
		rect: [0,90,44,134],
		frames: 1,
		duration: 3,
		attack_area: [4,18,24,28],
	},
	{
		id: "hurt",
		rect: [0,90,44,134],
		frames: 1,
		duration: 18,
	},
],
object_type: {
	id: "thrown",
	prototype: ["throwable_small_enemy"],
	properties: {
		acquirable_item_drop_value: 10,					
		points_value: 14,
		default_anim: "'fly1'",
	},

	#specifically trigger death here because non-solid objects don't have a water-level
	on_enter_water: "[handle_death(null),die()]",
	on_die: "if(me.underwater, null, proto_event('throwable_small_enemy', 'die'))",
	animation: [
		{
			image: "enemies/mushroom-jellyfish.png",
			id: "thrown",
			thrown_area: "all",
			duration: 60,
			rect: [175,37,201,70],
			frames: 1,
			body_area: null,
		},
		{
			image: "enemies/mushroom-jellyfish.png",
			id: "on_back",
			rect: [175,37,201,70],
			frames: 2,
			pad:2,
			duration: 5,
			body_area: "all",
		},
	],
},
}
